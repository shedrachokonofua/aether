# Blockchain Stack - Bitcoin + Monero + Fulcrum + LND (NixOS)
# Self-hosted cryptocurrency infrastructure for privacy and sovereignty
#
# Storage:
#   - Root disk (Ceph RBD): OS, LND, Fulcrum index (~150GB NVMe)
#   - Blockchain data: NFS mount from Smith HDD pool (~800GB)
#
# Services:
#   - bitcoind: Bitcoin full node
#   - monerod: Monero full node
#   - Fulcrum: Electrum server (address index)
#   - LND + ThunderHub: Lightning Network (added after sync)
#
# Deploy NixOS config after provisioning:
#   task configure:blockchain-stack

# NixOS cloud-config with machine certificate for OpenBao cert auth
module "blockchain_stack_cloud_config" {
  source = "./modules/nixos_cloud_config"

  name                 = local.vm.blockchain_stack.name
  ip_addresses         = [local.vm.blockchain_stack.ip]
  node_name            = local.vm.blockchain_stack.node
  provisioner_password = var.secrets["step_ca.provisioner_password"]
}

resource "proxmox_virtual_environment_vm" "blockchain_stack" {
  vm_id       = local.vm.blockchain_stack.id
  name        = local.vm.blockchain_stack.name
  node_name   = local.vm.blockchain_stack.node
  description = "Blockchain Stack - Bitcoin + Monero + Fulcrum + LND"

  stop_on_destroy = true

  cpu {
    cores = local.vm.blockchain_stack.cores
    type  = "host"
  }

  memory {
    dedicated = local.vm.blockchain_stack.memory
  }

  network_device {
    bridge  = "vmbr0"
    vlan_id = 3
  }

  disk {
    datastore_id = "ceph-vm-disks"
    file_id      = local.nixos_vm_image
    size         = local.vm.blockchain_stack.disk_gb
    interface    = "virtio0"
    iothread     = true
    discard      = "on"
  }

  initialization {
    datastore_id = "ceph-vm-disks"

    ip_config {
      ipv4 {
        address = "${local.vm.blockchain_stack.ip}/24"
        gateway = local.vm.blockchain_stack.gateway
      }
    }

    dns {
      servers = [local.vm.blockchain_stack.gateway]
    }

    user_data_file_id = module.blockchain_stack_cloud_config.file_id
  }

  lifecycle {
    prevent_destroy = true
    ignore_changes  = [disk[0].file_id, initialization[0].user_data_file_id]
  }

  # No HA - depends on Smith NFS for blockchain data.
  # LND channel recovery via Static Channel Backups to PBS, not HA.
}

# =============================================================================
# OpenBao Secrets
# =============================================================================
# Generated by Terraform, synced to OpenBao KV â†’ vault-agent on VM

# Bitcoin RPC credentials (auto-generated)
resource "random_password" "bitcoind_rpc" {
  length  = 32
  special = false
}

resource "random_id" "bitcoind_rpc_salt" {
  byte_length = 16
}

# Compute HMAC-SHA256 for rpcauth (same algorithm as bitcoin-rpcauth script)
data "external" "bitcoind_rpcauth" {
  program = ["python3", "-c", <<-PYTHON
import hmac
import hashlib
import json

salt = "${random_id.bitcoind_rpc_salt.hex}"
password = "${random_password.bitcoind_rpc.result}"
hmac_hash = hmac.new(
    salt.encode('utf-8'),  # Salt as string bytes, NOT binary!
    password.encode('utf-8'),
    hashlib.sha256
).hexdigest()

# Output: fulcrum:salt$hash (single $ between salt and hash)
result = "fulcrum:" + salt + "$" + hmac_hash
print(json.dumps({"rpcauth": result}))
PYTHON
  ]
}

resource "vault_kv_secret_v2" "bitcoind" {
  mount = vault_mount.kv.path
  name  = "aether/bitcoind"

  data_json = jsonencode({
    rpc_auth     = data.external.bitcoind_rpcauth.result.rpcauth
    rpc_password = random_password.bitcoind_rpc.result
  })
}

# ThunderHub password (auto-generated)
resource "random_password" "thunderhub" {
  length  = 32
  special = false
}

resource "vault_kv_secret_v2" "lnd" {
  mount = vault_mount.kv.path
  name  = "aether/lnd"

  data_json = jsonencode({
    thunderhub_password = random_password.thunderhub.result
  })
}
